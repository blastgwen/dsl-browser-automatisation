grammar com.selenium.gram.xtext.SlnDsl with org.eclipse.xtext.common.Terminals

generate slnDsl "http://www.selenium.com/gram/xtext/SlnDsl"

/*
 * Le modele
 */
Model:
	subs+=Subprocedure*
	'main' 
	defs+=Definition*
	main+=Instruction+ 
	'endmain'
;
	
Subprocedure:
	'sub' head=Head 
	defs+=Definition*
	body+=Instruction+	
	'endsub'
	;

Head:
	name=FunctionName '()' 
	| name=FunctionName '(' 'var' argsID+=VariableName (',' 'var' argsID+=VariableName )* ')'
;

/*
 * Subprocedure
 */
FunctionReference:
	functionName=[FunctionName]
;

FunctionName:
	name=ID
;

FunctionCall:
	ref=FunctionReference '()' 
	| ref=FunctionReference '(' args+=Expression (',' args+=Expression)* ')'
;

/*
 * Variables
 */
VariableReference:
	varID=[VariableName]
;

VariableName:
	name=ID
;

NumberLiteral:
	INT | STRING
;

/*
 * Expression
 */

Expression:
	VariableReference
	| NumLiteralExpression
	| ListExpression
	| BooleanExpression
	| ActionSelectExpression
;	

NumLiteralExpression:
	value = NumberLiteral
;

ListExpression :
	'sizeof' '(' exp=Expression ')'
;

BooleanListExpression :
	 op=BooleanListOperator '(' 'var'  varName=VariableName '->' exp=Expression ')'
;

BooleanListOperator:
	'All' | 'Any'
;

BooleanExpression :
	exp=BinaryBooleanExpression
	| exp=NegationExpression
	| exp=VerifyAction
	| exp=ExistAction
	| exp=BooleanListExpression
;

VerifyAction :
	'verify' '(' nameElement=NumberLiteral ',' value=Expression ')'
;

ExistAction :
	'exist' '(' nameElement = NumberLiteral ')'
;

BinaryBooleanExpression:
	op=BooleanOperator left=Expression right=Expression
;

NegationExpression :
	'!' exp = Expression
;

BooleanOperator :
	('=='|'<'|'>')
;

/*
 * Type of Instruction
 */
Instruction:
	FunctionCall 
	| Conditional
	| Loop
	| ActionInstruction
	| Assignation
;

Definition:
	'var' varID=VariableName '=' exp=Expression
;

Assignation:
	var=VariableReference '=' exp=Expression
;

Conditional:
	'if' exp=Expression 'then' trueIns+=Instruction+ ('else' falseIns+=Instruction+)? 'endif'
;

/*
 * Loop et Conditional
 */

Loop:
	While
	| Foreach
;

While:
	'while' '(' cond=Expression ')' 'do' ins+=Instruction+ 'endwhile'
;
Foreach:
	'foreach' '(' var=VariableName 'in' exp=Expression ')' 'do' ins+=Instruction+ 'endfor'
;

ActionInstruction:
	action=ActionOpen
	| action=ActionClick
	| action=ActionCheck
	| action=ActionType
;

ActionClick:
	'click' '('type=ClickableElement ',' element=NumberLiteral ')'
;

ActionOpen:
	'open' '(' url=URL ')'
;

ActionCheck:
	'check' '('type=ClickableElement ',' element=NumberLiteral')'
;

ActionType:
	'type' '(' nameElement=NumberLiteral ',' value=Expression ')'
;
ActionSelectExpression :
	'select' '(' element=Expression ')'
;

terminal ClickableElement:
	'button'|
	'image'|
	'link'
;
/*
 * URL
 */
terminal URL:
    
    ('a'..'z')+ '://' // Protocol
 
    ( // Hostname
        (
            ('0'..'9')+ '.' ('0'..'9')+ '.' ('0'..'9')+ '.' ('0'..'9')+
        ) |
        (
            ('a'..'z'|'A'..'Z')
            (
                'a'..'z'|'A'..'Z'|
                '0'..'9'|
                '$'|
                '-'|
                '_'|
                (
                    '\\'
                    ('0'..'9'|'a'..'f'|'A'..'F')
                    ('0'..'9'|'a'..'f'|'A'..'F')
                ) |
                (
                    '%'
                    ('0'..'9')
                    ('0'..'9')
                )
            )*
            (
                '.'
                ('a'..'z'|'A'..'Z')
                (
                    'a'..'z'|'A'..'Z'|
                    '0'..'9'|
                    '$'|
                    '-'|
                    '_'|
                    (
                        '\\'
                        ('0'..'9'|'a'..'f'|'A'..'F')
                        ('0'..'9'|'a'..'f'|'A'..'F')
                    ) |
                    (
                        '%'
                        ('0'..'9')
                        ('0'..'9')
                    )
                )*
            )*
        )
    )
    (':' ('0'..'9')+)? // Portnumber
 
    ('/'
        (
            'a'..'z'|'A'..'Z'|
            '0'..'9'|
            '$'|
            '-'|
            '_'|
            '~'|
            '+'|
            '.'|
            (
                '\\'
                ('0'..'9'|'a'..'f'|'A'..'F')
                ('0'..'9'|'a'..'f'|'A'..'F')
            ) |
            (
                '%'
                ('0'..'9')
                ('0'..'9')
            )
        )*
    )* // Path
 
    ('?'
        (
            'a'..'z'|'A'..'Z'|
            '0'..'9'|
            '$'|
            '-'|
            '_'|
            '='|
            '&'|
            ';'|
            (
                '\\'
                ('0'..'9'|'a'..'f'|'A'..'F')
                ('0'..'9'|'a'..'f'|'A'..'F')
            ) |
            (
                '%'
                ('0'..'9')
                ('0'..'9')
            )
        )+
 
        ('+'
            (
                'a'..'z'|'A'..'Z'|
                '0'..'9'|
                '$'|
                '-'|
                '_'|
                '='|
                '&'|
                ';'|
                (
                    '\\'
                    ('0'..'9'|'a'..'f'|'A'..'F')
                    ('0'..'9'|'a'..'f'|'A'..'F')
                ) |
                (
                    '%'
                    ('0'..'9')
                    ('0'..'9')
                )
            )+
        )*
    )?; 