grammar com.selenium.gram.xtext.SlnDsl with org.eclipse.xtext.common.Terminals

generate slnDsl "http://www.selenium.com/gram/xtext/SlnDsl"

/*
 * Le modele
 */
Model:
	navigator=SelectBrowser
	subs+=Subprocedure*
	'main' 
	body=Body 
	'endmain'
;

SelectBrowser:
	'browser' browser=BROWSER 
;

BROWSER:
	'firefox' | 'chrome' 
;
	
Subprocedure:
	'sub' head=Head 
	body=Body	
	'endsub'
	;

Body:
	defs+=Definition*
	instructions+=Instruction+
;

Head:
	name=FunctionName '()' 
	| name=FunctionName '(' 'var' argsID+=VariableName (',' 'var' argsID+=VariableName )* ')'
;

/*
 * Subprocedure
 */
FunctionReference:
	functionName=[FunctionName]
;

FunctionName:
	name=ID
;

FunctionCall:
	ref=FunctionReference '()' 
	| ref=FunctionReference '(' args+=Expression (',' args+=Expression)* ')'
;

/*
 * Variables
 */
VariableReference:
	varID=[VariableName]
;

VariableName:
	name=ID
;

NumberLiteral:
	INT | STRING
;

Uri:
	url=URL
;

/*
 * Expression
 */

Expression:
	VariableReference
	| NumLiteralExpression
	| SizeOfExpression
	| BooleanExpression
	| SelectAction
	| Uri	
;	

NumLiteralExpression:
	value = NumberLiteral
;

SizeOfExpression :
	'sizeof' '(' exp=Expression ')'
;

BooleanListExpression :
	 op=BooleanListOperator '(' 'var'  varName=VariableName '->' exp=Expression ')'
;

BooleanListOperator:
	'All' | 'Any'
;

BooleanExpression :
	exp=BinaryBooleanExpression
	| exp=NegationExpression
	| exp=VerifyAction
	| exp=ExistAction
	| exp=BooleanListExpression
	| exp=BooleanValue
	| exp=BinaryLogicalExpression
	| exp=VariableReference
;

BooleanValue:
	value=BOOLEAN
;

terminal BOOLEAN:
	'true' | 'false'
;

VerifyAction :
	'verify' '(' verifyType=VerifiableElement ',' nameElement=Expression ',' value=Expression ')'
;

ExistAction :
	'exist' '(' select=SelectableElement ',' nameElement=Expression ')'
;

SelectAction :
	'select' '(' type=SelectableElement ',' nameElement=Expression ')'
;

BinaryBooleanExpression:
	op=BooleanOperator left=Expression right=Expression
;

BinaryLogicalExpression:
 	op=LogicalOperator left=BooleanExpression right=BooleanExpression 
;

NegationExpression :
	'!' negation = BooleanExpression
;

LogicalOperator:
	'&&' | '||'
;

BooleanOperator :
	('=='|'<'|'>')
;


/*
 * Type of Instruction
 */
Instruction:
	FunctionCall
	| Conditional
	| Loop
	| ActionInstruction
	| Assignation
;

Definition:
	'var' varID=VariableName '=' exp=Expression
;

Assignation:
	var=VariableReference '=' exp=Expression
;

Conditional:
	'if' exp=BooleanExpression 'then' trueIns+=Instruction+ ('else' falseIns+=Instruction+)? 'endif'
;

/*
 * Loop et Conditional
 */

Loop:
	While
	| Foreach
;

While:
	'while' '(' cond=BooleanExpression ')' 'do' ins+=Instruction+ 'endwhile'
;
Foreach:
	'foreach' '(' var=VariableName 'in' exp=Expression ')' 'do' ins+=Instruction+ 'endfor'
;

ActionInstruction:
	action=ActionOpen
	| action=ActionClick
	| action=ActionCheck
	| action=ActionType
;

ActionClick:
	'click' '(' type=ClickableElement ',' element=Expression ')'
;

ActionOpen:
	'open' '(' url=Expression ')'
;

ActionCheck:
	'check' '('element=Expression ',' value=BooleanExpression')'
;

ActionType:
	'type' '(' nameElement=Expression ',' value=Expression ')'
;
	
SelectableElement:
	ClickableElement | VerifiableElement
;

ClickableElement:
	'button'|'image'| 'link';

VerifiableElement:
	'checkbox' | 'textbox'
;

/*
 * URL
 */
terminal URL:
    
    ('a'..'z')+ '://' // Protocol
 
    ( // Hostname
        (
            ('0'..'9')+ '.' ('0'..'9')+ '.' ('0'..'9')+ '.' ('0'..'9')+
        ) |
        (
            ('a'..'z'|'A'..'Z')
            (
                'a'..'z'|'A'..'Z'|
                '0'..'9'|
                '$'|
                '-'|
                '_'|
                (
                    '\\'
                    ('0'..'9'|'a'..'f'|'A'..'F')
                    ('0'..'9'|'a'..'f'|'A'..'F')
                ) |
                (
                    '%'
                    ('0'..'9')
                    ('0'..'9')
                )
            )*
            (
                '.'
                ('a'..'z'|'A'..'Z')
                (
                    'a'..'z'|'A'..'Z'|
                    '0'..'9'|
                    '$'|
                    '-'|
                    '_'|
                    (
                        '\\'
                        ('0'..'9'|'a'..'f'|'A'..'F')
                        ('0'..'9'|'a'..'f'|'A'..'F')
                    ) |
                    (
                        '%'
                        ('0'..'9')
                        ('0'..'9')
                    )
                )*
            )*
        )
    )
    (':' ('0'..'9')+)? // Portnumber
 
    ('/'
        (
            'a'..'z'|'A'..'Z'|
            '0'..'9'|
            '$'|
            '-'|
            '_'|
            '~'|
            '+'|
            '.'|
            (
                '\\'
                ('0'..'9'|'a'..'f'|'A'..'F')
                ('0'..'9'|'a'..'f'|'A'..'F')
            ) |
            (
                '%'
                ('0'..'9')
                ('0'..'9')
            )
        )*
    )* // Path
 
    ('?'
        (
            'a'..'z'|'A'..'Z'|
            '0'..'9'|
            '$'|
            '-'|
            '_'|
            '='|
            '&'|
            ';'|
            (
                '\\'
                ('0'..'9'|'a'..'f'|'A'..'F')
                ('0'..'9'|'a'..'f'|'A'..'F')
            ) |
            (
                '%'
                ('0'..'9')
                ('0'..'9')
            )
        )+
 
        ('+'
            (
                'a'..'z'|'A'..'Z'|
                '0'..'9'|
                '$'|
                '-'|
                '_'|
                '='|
                '&'|
                ';'|
                (
                    '\\'
                    ('0'..'9'|'a'..'f'|'A'..'F')
                    ('0'..'9'|'a'..'f'|'A'..'F')
                ) |
                (
                    '%'
                    ('0'..'9')
                    ('0'..'9')
                )
            )+
        )*
    )?; 